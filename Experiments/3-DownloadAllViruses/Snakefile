viruses, = glob_wildcards('data/fastas/{virus}.fna.gz')

rule all:
    input:
        'data/rscu_correlations/all_correlations_total_human.csv',
        'data/rscu_correlations/all_correlations_total_mosquito.csv'

rule create_codon_count_tables:
    input:
        file = 'data/fastas/{virus}.fna.gz'
    output:
        codon_counts = 'data/codon_tables/codon_counts/{virus}.csv',
        codon_counts_total = 'data/codon_tables/codon_counts_total/{virus}.csv'
    threads: 40
    script:
        'scripts/create_codon_count_tables.R'

rule concatenate_codon_counts:
    input:
        counts = expand('data/codon_tables/codon_counts/{virus}.csv', virus = viruses),
        counts_total = expand('data/codon_tables/codon_counts_total/{virus}.csv', virus = viruses)
    output:
        all_counts = protected('data/codon_tables/all_codon_counts.csv'),
        all_counts_total = protected('data/codon_tables/all_codon_counts_total.csv')
    script:
        'scripts/concatenate_codon_counts.R'

rule create_rscu_tables:
    input:
        codon_counts = 'data/codon_tables/codon_counts/{virus}.csv',
        codon_counts_total = 'data/codon_tables/codon_counts_total/{virus}.csv'
    output:
        rscu = 'data/rscu_tables/rscu/{virus}.csv',
        rscu_total = 'data/rscu_tables/rscu_total/{virus}.csv'
    threads: 40
    script:
        'scripts/create_rscu_tables.R'

rule concatenate_rscu_tables:
    input:
        rscu = expand('data/rscu_tables/rscu/{virus}.csv', virus = viruses),
        rscu_total = expand('data/rscu_tables/rscu_total/{virus}.csv', virus = viruses)
    output:
        all_rscu = protected('data/rscu_tables/all_rscu.csv'),
        all_rscu_total = protected('data/rscu_tables/all_rscu_total.csv')
    script:
        'scripts/concatenate_rscu_tables.R'

rule calculate_rscu_ratios:
    input:
        all_rscu = 'data/rscu_tables/all_rscu.csv',
        all_rscu_total = 'data/rscu_tables/all_rscu_total.csv'
    output:
        rscu_ratio_human = protected('data/rscu_ratios/all_rscu_ratio_human.csv'),
        rscu_ratio_total_human = protected('data/rscu_ratios/all_rscu_ratio_total_human.csv'),
        rscu_ratio_mosquito = protected('data/rscu_ratios/all_rscu_ratio_mosquito.csv'),
        rscu_ratio_total_mosquito = protected('data/rscu_ratios/all_rscu_ratio_total_mosquito.csv')
    threads: 20
    script:
        'scripts/calculate_rscu_ratios.R'

rule calculate_correlations:
    input:
        human_ratios = 'data/rscu_ratios/all_rscu_ratio_total_human.csv',
        mosquito_ratios = 'data/rscu_ratios/all_rscu_ratio_total_mosquito.csv'
    output:
        human_corr = 'data/rscu_correlations/all_correlations_total_human.csv',
        mosquito_corr = 'data/rscu_correlations/all_correlations_total_mosquito.csv'
    threads: 40
    script:
        'scripts/calculate_correlations.R'
